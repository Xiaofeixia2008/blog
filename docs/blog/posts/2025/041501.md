---
title: 记分板(2)
date: 
  created: 2025-04-15
  updated: 2025-04-21
categories:
  - MC开发
tags:
  - 记分板
slug: scoreboard
description: 学习了记分板之后的总结和感想
readtime: 25
links:
  - 2.4.5 记分板的运用 变量 | 量筒的 Minecraft 基岩版开发文档: https://yzbwdlt.pages.dev/docs/commands/chapter2/section4/subsection5
---

好不容易，终于把标签和记分板的前面几节啃下来了，剩下的最后这一节是......习题课。但是这节的综合性未免有点强了吧......甚至还要串联起前面的`execute`和目标选择器。~~有些知识已经忘了qwq~~

<!-- more -->

## 如何检测分数？

关于这个问题，上一节学习了一个`/scoreboard players test`，它就能检测分数是否为特点值，或是否在一个特定范围内。但它的检测结果只能输出在聊天栏中，无法用于对其他命令进行选择性执行。那么在本节的开头，就介绍了另外两种检测分数的方法*习题课居然还有新知识......*，一个目标选择器参数，一个`execute`的子命令。

### 目标选择器

语法为`scores={<记分项: string>=<值: integer range>,...}`，它是继`hasitem`之后我学到的第二个比较~~抽象~~的选择器参数，因为它也用到了大括号，在实际使用中一定要特别注意。它会在**指定**的记分项上寻找符合数值范围的玩家（或实体），**不包括假名**。若要检测一个数值，直接放上去即可；如果是一个闭区间，那在最小值和最大值之间用`..`连接，不需要空格！若一端为无穷，则那个位置留空即可。当然，它是支持反选的，在前面加一个`!`就可以了。

这里就问了个问题，如果现在分数是15，那`scores={data=!10..}`能不能检测通过呀？要检测的是x\|x∉\[10,+∞)这个集合，15都大于10了，肯定不在检测的范围内啊，所以检测不会通过。这部分内容还是比较简单的了。

### `execute`的子命令

这个就比选择器高级多了，甚至说，选择器能够做到的，它也能做到。它最显著的优势就是：**可以指定假名**。道理也很简单：目标选择器必须选中一个实体，所以不能用假名；这个子命令是检测分数若在某范围中，则继续运行execute命令。所以哪怕是假名，只要它的分数能够检测通过，那么命令就会继续向下运行。

其实`if score`有两条语法。首先看第一条，它可以比较两个变量间的关系。先是第一个变量，然后是"操作方法"，最后是第二个变量。乍一看和上一节的`operation`有点相似，但是操作符部分的差异还是有的。另外要注意，`operation`的两个变量顺序不能交换（对调操作除外），改变的是第一个变量；而`if score`不会改变变量的值，所以两个变量可以对换位置，只要操作符正确就行。另外一条语法用来比较一个变量是否在特定区间内，也是熟悉的语法：先是变量名，接着一个`matches`，最后就是要检测的数值或范围。数值范围的表示方法和选择器的相同，也支持反选。

学习完新知识之后，又抛出了一道思考题，这道题目比较有意思，这里把原题附上：

> 你可以只用`/scoreboard players operation`命令，完成两个分数的大小比较吗？比如，当`data.x`>`data.y`时，执行命令`/say 1`，但是不能用`/execute if score x data > y data`去检测！

```mcfunction {linenums="1" title="我的作答"}
scoreboard players operation z data = x data
scoreboard players operation z data -= y data
execute if score z data matches 1.. run say 114514
```

《你可以只用》，考验语文的时候到了......这里用到了数学课学过的小技巧：比较两个数的大小，可以做差后再比较。

### 一点小细节

表示检测的范围时有一点要注意，如果要检测的范围是多个集合的并集，且它们的并集中有不连续的地方，这样的话没有办法一步到位进行检测。要记住，它们只有和(And)的关系，没有或(Or)的关系！在此借用一下量筒的话：

> - 要实现或的关系，要么多条命令
> - 要么集合计算
> - 没有捷径

文字描述可能有点啰嗦，接下来举2个例子，体会一下如何实现"或"的操作。（假定要检测的分数在`foo`记分项上）

1. 分数为0分或100分。
  - 目标选择器：
    `scores={foo=0..100,foo=!1..99}`
  - execute命令：
    `if score xx foo matches 0..100 unless score xx foo matches 1..99`
2. 分数为0到10分，或为90到100分。
  - 目标选择器：
    `scores={foo=0..100,foo=!11..89}`
  - execute命令：
    `if score xx foo matches 0..100 unless score xx foo matches 11..89`

## 正片开始了！

该学的学完了，接下来就是真正的习题课了。教程给出了10个例子（怎么这么多......），下面我来逐一进行复盘。

### 信息板

注意啦，这个可不是单纯的记分功能，在地图《30种死法 2》中就可以看到这种信息板。你会注意到，红色的分数并不会改变，变化的是分数左边的假名。看似很高级，相信大家在第一眼看到的时候会以为这些假名是通过计算、拼接字符串得出的。事实上真相并非如此。这些假名的显示，是通过不同的状态量进行枚举的。这带来了一个弊端：在状态量很多的时候，由于需要进行大量的移除显示分数、比较分数的操作，对设备的性能要求较大，也会拉低代码的运行效率。另外在《量筒的起床战争》中，所谓的"记分板"并没有分数。群友解析："就是actionbar的右侧形态"。这个等后面学到了再进行讨论吧......

### 抽奖机

其实，最原始的抽奖机不需要依靠命令，一种主流的思路是借助发射器、投掷器输送物品的随机性。但是有一个问题：它们的物品栏槽位只有9个，如果奖品有10个甚至更多，或是想自定义某些物品抽中的概率，以及一些更复杂的情况时，这种方法就不奏效了。

要借助记分板来实现抽奖的话，那当然得用上`random`啦。这个随机的范围要怎么定呢？首先，起点最好是1，方便数据计算、处理。随机范围的终点就有讲究了：如果奖品是等可能抽中的，那就是奖品总种类数。如果不是等可能性的话，首先把每种情况的概率用分数（是数学上的分数，有分子分母的那个）表示出来，然后通分。这样随机终点就是通分后的公分母了。

暂时先抛开记分板，来看看如何判断与抽奖机交互的玩家，以及如何扣除物品的问题。由于目前所学的知识有限，目前能够实现的功能并不完美。检测玩家，使用选择器的`@p`配合`hasitem`参数，而清除物品使用`clear`命令即可。为了简化命令，可以给符合条件的玩家打个标签，这样就不用在每条命令都写个hasitem了。但是如果进行一次抽奖要花费多个物品，或者是进行抽奖的玩家离抽奖机不是最近，那这些方法就用不了了。不过这已经是后话了......

### 随机对话

实际上这个的核心逻辑和抽奖机大差不差。只是抽奖机在检测分数之后执行给予物品的命令，而这个后面就是执行说话的命令了。

### 检测玩家的不同状态

这里的"状态"，包括站立、潜行、爬行和睡觉。什么？你问游泳和滑翔为什么不在其中？其实这两个情况都相当于爬行。那处于骑乘状态呢？我暂时没有在wiki中找到相关描述，但是在游戏内，看起来像是潜行。

检测这些不同状态的原理是基于碰撞箱高度不同。像站的时候有1.8格高，而睡觉的时候只有可怜的0.15格了。（你问为什么爬行和睡觉人都是平的，但是高度差距怎么大？我也不知道......）要检测的话，用`@s`选择器配合`y`和`dy`参数（记住y轴就是垂直于地面的那个），使用相对坐标即可。在套execute命令的时候不要忘了加上`at @s`来设置执行位置。

检测好了，如果要记录下状态，要根据实际需求使用标签或者记分板。如果只要检测玩家是否处于某个状态，使用标签。如果要检测多个状态，用记分板。说白了就是：如果只有2个状态，用标签；如果有3个及以上的状态，用记分板。

最后给了个小要求，让我补齐检测爬行和睡觉的命令。这好办，已经有了检测前两个的命令，剩下的改改数字就好了。

```mcfunction {linenums="1" title="检测玩家的不同状态"}
execute as @a at @s if entity @s[y=~1.6,dy=0.1] run scoreboard players set @s state 0 # (1)!
execute as @a at @s if entity @s[y=~1.3,dy=0.1] unless entity @s[y=~1.6,dy=0.1] run scoreboard players set @s state 1 # (2)!
execute as @a at @s if entity @s[y=~0.4,dy=0.1] unless entity @s[y=~1.3,dy=0.1] run scoreboard players set @s state 2 # (3)!
execute as @a at @s unless entity @s[y=~0.4,dy=0.1] run scoreboard players set @s state 3 # (4)!
```

1. 玩家站立时的碰撞箱高度为1.8格。
2. 玩家潜行时的碰撞箱高度为1.49格。[**仅BE**]
3. 玩家爬行时的碰撞箱高度为0.6格。
4. 玩家睡觉时的碰撞箱高度为0.15格。[**仅BE**]

以上代码中，玩家的状态被储存在`state`记分项中。0为站立，1为潜行，2为爬行，3为睡觉。

### 基于记分板的商店

要实现商店的功能，本质上就是先检测货币够不够，够了再进行扣款和给予物品。所以抽奖机的一部分逻辑也可以用于商店功能。但是上面也说过了，抽奖机的逻辑还有改进空间，因此基于记分板的商店或许是个更好的选择。

似乎利用记分板的话逻辑还更简单。如果分数小于东西的价格，就提示货币不足；如果分数是大于或等于价格，那就给东西、扣款。

/// admonition | 一个小彩蛋（？）
    type: tip

教程的这一节有条命令，是这样子的：

`execute as @a unless score @s money matches 1000.. run give @s 你没有足够的货币购买钻石剑！`

你发现了什么有趣的地方么？（坏笑）

后话：它已经被更正了，现在已经没办法在教程里面看到了。
///

### 计时器

习题课的旅程已经过半。后面这5个例子，由于有些在之前的课程中已经了解过，所以甚至理解起来还更容易。计时器就是一个典例。

最简单的计时器只需要循环执行一条命令：令变量自增1。一秒有20刻，而每刻执行一次命令，这样经过的时间用秒钟表示的话就是示数除以20。例如，在aw5（筑梦）中的小游戏计时器就是如此的简单。（~~其实是因为作者懒吧~~）

想要高级点，能够显示时分秒？其实逻辑也很简单。如果刻数满了20就把秒数进1，并把刻数减去20。分和时以此类推，不过要记住它们的进率是60。

### 获取实体数目

乍一看挺复杂的，事实上，只要把关键的思路找准了，问题就迎刃而解了。先问一个简单的问题：如何使所有玩家都说一句话？在execute那一节，这种题是非常经典了。现在要统计玩家数量，是不是有点感觉了？首先把变量设为0，然后让全部玩家都在这个变量上面加1，就是玩家的数目了。如果要统计特定条件的玩家，或者是实体，都可以，只要把选择器改一下就可以了。嘿嘿，你别说还挺巧妙的。

### 处理僵尸玩家

这里的"僵尸玩家"是指，如果玩家在线的时候有分数在记分板上，那他的分数还会留在记分板上，不过名字变成了"玩家下线"。但是如果我们要把记分板显示出来的话，我们并不希望显示"玩家下线"的分数（一大排玩家下线多难看啊）。这个时候就需要两个记分项了：一个记录所有玩家（包括玩家下线）的数据，而另外一个用来显示在线玩家的数据。那很好了，记分板有个通配符`*`可以选中全部追踪对象，只要在显示用的记分项中先把`*`的分数删掉，再把`@a`的分数加回来就行了。

但是如果玩过的人太多了，记分板记载的数据太多了，就会导致性能问题：在移除分数的时候会导致卡顿（在冈易租赁服比较常见）。这个时候就有一个更为直接，也更加简单粗暴的方法：把整个记分项删掉。反正再加回来就好了，要显示给玩家看，那就在加完记分项之后再加个显示的指令就完事了。关于记分板删了再新建这个操作带来的屏闪，留待后面再讨论吧。（这已经是第几次"后面再讨论"了啊......）虽然但是，刚刚不才说到了计时器嘛，那搞成每隔一段时间再执行删记分板的命令，好像也可以耶......但愿如此吧......

### 检测玩家的退出重进

这个也是搞开发的必备技能了。如果你有检测重进玩家的需求，那你一定会用上检测重进玩家的命令。~~*（这是什么废话文学啊）*~~好像*（不要怀疑自己，就是有这一道题）*在上一节的练习题也有类似的，不过当时是给了文字描述，要给翻译成命令。具体的实现思路如下：

1. 将所有在线玩家的分数加上0。
2. 移除全部有分数的玩家（包括下线的）。
3. 将所有在线玩家的分数设为1。

如果玩家是在线的，那么他的分数会一直为1，如果玩家下线了，通过命令2将其分数移除。如果有玩家刚上线，先执行命令1，因为他是没有分数的，那在进行分数加0的时候按照默认值0进行操作，因此执行完后他的分数是0。正常玩家由于有命令3的赋值，在经过命令1之后分数为1。所以就可以把刚上线的玩家筛出来了。刚上线的玩家在执行完后面两条命令之后，分数变成了1，也就成了在线玩家。

这方法虽然道理很简单，命令也很简洁，但是还是有两点需要注意：

- 三条命令顺序不可更改！  
  这个的道理也是很简单的，重进的玩家没有分数，在加0之后就和在线玩家的分数有差别了。如果尝试着改变一下它们的顺序，你会发现是解释不通的。
- 重进玩家需执行的命令要在命令1之后执行！  
  这一点也需要注意。在命令1执行之后分数有0和1的区别。但是在命令2和3执行之后，无论是刚进的玩家还是已经在线的玩家，要么没有分数，要么就都是1，这显然是行不通的。

这一部分的最后问了个问题，如果玩家太多导致删分数的时候卡顿，怎么办？上一个例子刚刚说过了，只要把整个记分项删了再加回来就行。

### 死亡榜

最后一个实例，也是重中之重啊。之前已经了解过如何利用标签来标记玩家是否存活、死亡。要点是`@a`选择器的选择无论死活，而`@e`只能选中存活的。但是如果是检测到存在死亡的标签就给这个玩家的死亡次数加1，那只要他不点重生，就一直处于死亡状态，死亡次数也就会噌噌往上涨。因此标签已经不行了，需要记分板派上用场了。这时需要3个状态：

- 0：存活
- 1：刚死（死亡了但未被记录）
- 2：持续死（死亡了且已经被记录）

这个时候精确的死亡榜的实现思路已经呼之欲出了，下面把主要逻辑用文字表述出来，供大家体会。注："所有玩家"就是指用`@a`能够检测到的玩家，无论死活。

- 将所有非2玩家设为1。  
  持续死亡的玩家当然不能再被设为刚死亡了。
- 将存活玩家设为0。  
  在上一步之后，存活玩家应该是1，现在把他们设置为正确的分数。
- 将所有1的玩家的死亡数加1。  
  存活和持续死亡的玩家将不会增加死亡数。
- 将所有1的玩家设为2。  
  将刚死的玩家设置为持续死亡，因为已经记录了死亡数。

---

一下就塞了这么多知识，肯定没办法全部都学透，先做一下练习题吧，顺便也能检测一下我的知识漏洞。

## 练习题部分

作为习题课，这次的12道练习题（似乎上一节也是12道题......），分明是上强度了，就前2题是单命令题目，其余均为多命令题目。姑且就分为前面的10道必做题和后面的2道选做题吧。特别感谢量筒，他对我所有的答案进行了仔细的检查，并且对出现的错误或者待改进之处提出了建议。

//// details | 关于"我的作答"
    type: info

由于量筒为每道题的作答都给出了详细的分析，我将在"我的作答"部分根据每题的作答情况，使用不同类型的提示框将量筒的分析置于其中。具体如下：

/// admonition | 正确
    type: success

作答与参考答案一致。或是使用了不同的方法，但是语法正确，且逻辑与答案相同。  
*你知道吗：若作答情况有且仅有"`say`部分与答案不一致"或类似说法，也归入此类别。*
///

/// admonition | 轻微出错（大致正确）
    type: question

作答出现了微小的语法错误，但是其逻辑没有出错。或实现了预期的行为，但是存在优化的空间。
///


/// warning | 中度出错
作答出现了较明显的语法错误，或是出现了较小的逻辑错误，致使执行结果部分偏离预期。
///

/// admonition | 错误
    type: failure

作答出现了较严重的语法错误，或是出现了较严重的逻辑错误，致使执行结果严重偏离预期。
///
////

/// details | 真题回顾
    type: note

1. 现在我们回顾练习 2.3-2 的第一个问题：  
   在地图《30 种死法 2》的第 17 关中，有一个进度要求玩家放下所有刷怪蛋。这个进度要获取的条件有 5 条：  
   （1）所给出的箱子（位于(-1,22,85)）被玩家拿空；  
   （2）检测不到任何掉落物存在（防止玩家扔出物品触发进度）；  
   （3）检测到存在骷髅；  
   （4）检测不到物品栏有骷髅刷怪蛋的玩家；  
   （5）该进度尚未获取；  
   （6）上一个进度已经获取，  
   在当时，我们忽略了（5）和（6）的条件，这是因为当时我们还不能实现类似的效果，但现在可以了。假设该进度对应的变量为`advancement.adv1`，上一个进度对应的变量为`advancement.adv0`，并且标记：`-1`=未解锁，`0`=已解锁但未获取，`1`=已获取。当符合所有条件时，执行命令`/say 恭喜你获取进度！`。试写出该命令。已知可供用于检测的空箱子位于(10,5,7)。
2. 在地图《冒险世界：筑梦》中，有一个跑酷小游戏。地图使用`time.timeline`记录跑酷时间，用`data.parkour`记录跑酷最佳成绩。试写出一条命令，破纪录后则更新最佳成绩。注意：记录应该是时间越短越好！
3. 在 Hypixel 的起床战争中，4v4v4v4 模式下设定了 14 人为开启游戏倒计时的最低人数阈值。试写 4 条命令，当人数大于等于 14 的时候，使`time.startCountdown`每刻减 1，当人数小于 14 的时候，将`time.startCountdown`复原回 400。
4. 在起床战争中，假定红队的床在(30,65,30)上，当床被破坏后则标记为红队的床被破坏。假定`data.redBedState`为标记红队床状态的变量，记`0`=被破坏，`1`=未被破坏，并且其初始值为`1`。写 2 条命令，当红队的床被破坏后公告全体玩家“§c红队的床已被破坏！”并标记红队的床为被破坏状态。
5. 在第 4 题的基础上，若一名带标签`teamRed`的玩家死亡后，并且红队的床处于破坏状态，则设置为被淘汰状态。假定标签`isEliminated`，当玩家拥有此标签时则代表被淘汰，反之则没有。试使用 3 条命令实现之。提示：如果该玩家已被淘汰，不必重复添加。
6. 假设接下来你要写的命令全部为每游戏刻执行一次。允许使用变量`time.tick`，该变量的初始值为`0`。试基于此变量，在 3 秒后对玩家说“Hello,world!”。[^1]
7. 若新进入服务器的玩家拥有标签`vip`，则全服公告“欢迎玩家 xxx 回到服务器”，自定义变量和记分板，用命令实现之。用`/say`实现公告效果。
8. 若玩家在一次跑酷中死亡超过 5 次，则公告“xxx 闯关失败”，自定义变量和记分板，用命令实现之。用`/say`实现公告效果。
9. 一个抽奖机，消耗一个泥土，有 0.01% 的概率抽出附魔金苹果，自定义变量和记分板，用命令实现之。
10. 试用命令检查处于死亡状态的人数，并输出到`data.deadPlayerAmount`上。
11. （难度较高，选做）现在假设你要做一个冰船竞速的小游戏，赛道成环，在赛道中有 3 个记录点：(-52,60,82)、(-4,60,76)、(-63,60,106)，玩家必须划船依次通过这 3 个记录点划 3 圈才算成功。允许使用记分板`boatRace`，试用命令写出你的思路。
12. （难度较高，选做）假设起床战争中有玩家在游戏中离开，重新进入后已是下一局，试分析如果什么也不做会导致什么问题？试用命令写出你的解决思路，允许额外定义变量。
///

[^1]:
    由于原题目的表述可能引起歧义，此题目已经被更改。原题目如下：  
      > 假设接下来你要写的命令全部为每游戏刻执行一次。允许使用变量`time.tick`，该变量的初始值为`0`。试基于此变量，在第 3 秒时对玩家说“Hello,world!”。

### 必做题

这前10道题，我还是做得有点吃力。因为综合性实在太强了，而且隐含的条件也不少，做题时方方面面都要兼顾到。

**第1题**，用`execute`，把各要求翻译成子命令，堆砌上去就对了。注意检测空箱子时不要忘了最后的`all`参数。

/// tab | 参考答案
```text {linenums="1"}
/execute if score adv1 advancement matches 0 if score adv0 advancement matches 1 if blocks -1 22 85 -1 22 85 10 5 7 all unless entity @e[type=item] if entity @e[type=skeleton] unless entity @a[hasitem={item=skeleton_spawn_egg}] run say 恭喜你获取进度！
```
///

/// tab | 我的作答
/// admonition | 结果分析
    type: success

`say`与答案不一致，其余正确。
///

```mcfunction {linenums="1"}
execute if blocks -1 22 85 -1 22 85 10 5 7 all unless entity @e[type=item] if entity @e[type=skeleton] unless entity @a[hasitem={item=skeleton_spawn_egg}] if score adv1 advancement matches 0 if score adv0 advancement matches 1 run say 114514 # (1)!
```

1. 按原题目要求，实际上最后一部分应该写成`run say 恭喜你获取进度！`。
///

---

**第2题**，需要实现若当前成绩打破了纪录，则更新成绩的功能，那么把问题分为两部分。首先，检测当前成绩与最好记录。题目也给出了提示，应该是当成绩**小于**记录的时候才是打破纪录。接下来是更新成绩，使用记分板`operation`的`=`操作符即可。

/// tab | 参考答案
```text {linenums="1"}
/execute if score timeline time < parkour data run scoreboard players operation parkour data = timeline time
```
///

/// tab | 我的作答
/// admonition | 结果分析
    type: success

正确。
///

```mcfunction {linenums="1"}
execute if score timeline time < parkour data run scoreboard players operation parkour data = timeline time
```
///

---

**第3题**，出现了2种不同的情况：若玩家人数大于等于14则将倒计时变量自减1；若玩家人数小于14则将变量设为400。限制了只能使用4条命令，那么现有的`data`记分项就要用起来。要获取玩家人数，直接运用教程中提供的思路，需2条命令。另外的2条命令就可以用来执行在不同人数下采取的措施了。数学课学过，这两种人数的情况互为补集。因此execute的条件子命令一个写`if`，一个写`unless`，后面分数范围的内容保持相同即可。

/// tab | 参考答案
```text {linenums="1"}
/scoreboard players set playerAmount data 0
/execute as @a run scoreboard players add playerAmount data 1
/execute if score playerAmount data matches 14.. run scoreboard players remove startCountdown time 1
/execute unless score playerAmount data matches 14.. run scoreboard players set startCountdown time 400
```
///

/// tab | 我的作答
/// admonition | 结果分析
    type: question

第二条语法`@e[type=player]`与答案不一致，但亦为正确写法；第四条语法`scoreboard`语法有误，应为`startCountdown >>data<< 400`。
///

第4行的错误有点隐蔽了。在`unless`的后面我漏掉了`score`，`startCountdown`的后面漏掉了`time`。你发现了吗？

```mcfunction {linenums="1" hl_lines="4"}
scoreboard players set populations data 0
execute as @e[type=player] run scoreboard players add populations data 1
execute if score populations data matches 14.. run scoreboard players remove startCountdown data 1
execute unless populations data matches 14.. run scoreboard players set startCountdown 400 # (1)!
```

1. 此行命令正确写法应为`execute unless score populations data matches 14.. run scoreboard players set startCountdown time 400`。
///

---

**第4题**，要在床被挖的时候更新记分板的状态并显示公告，同时限制了只能使用2条命令。很简单，检测同一个条件，但是两条命令做不同的事情。但是为了防止床被挖之后公告刷屏，公告的命令还需要加上一个条件：当床还没挖时执行。

/// tab | 参考答案
```text {linenums="1"}
/execute if block 30 65 60 air if score redBedState data matches 1 run say §c红队的床已被破坏！ # (1)!
/execute if block 30 65 60 air if score redBedState data matches 1 run scoreboard players set redBedState data 0
```

1. 注意要加上`if score`的检测，否则循环执行时会导致该队床在被破坏情况下不断公告。
///

/// tab | 我的作答
/// admonition | 结果分析
    type: question

基本正确，但有优化空间。  

使用了`unless block` ... `bed`，和答案并不一样，但是也是正确做法。

第二条命令会在检测到床之后不断执行，可以考虑套一个相同的`if score redBedState data matches 1`以使其只执行一次。
///

```mcfunction {linenums="1" hl_lines="2"}
execute unless block 30 65 30 bed if score redBedState data matches 1 run say 114514
execute unless block 30 65 30 bed run scoreboard players set redBedState data 0 # (1)!
```

1. 改进后的命令为：`execute unless block 30 65 30 bed if score redBedState data matches 1 run scoreboard players set redBedState data 0`
///

---

**第5题**，有4个条件：玩家死亡；带有标签；床已被挖；未被淘汰。检测死亡直接使用死亡榜的逻辑，使用2条命令，然后把这些条件用到同一个execute里面即可。

/// tab | 参考答案
```text {linenums="1"}
/tag @a remove isAlive
/tag @e[type=player] add isAlive
/execute as @a[tag=!isAlive,tag=teamRed,tag=!isEliminated] if score redBedState data matches 0 run tag @s add isEliminated
```
///

/// tab | 我的作答
/// admonition | 结果分析
    type: success

和答案一致。
///

```mcfunction {linenums="1"}
tag @a remove isAlive
tag @e[type=player] add isAlive
execute as @a[tag=!isAlive, tag=teamRed, tag=!isEliminated] if score redBedState data matches 0 run tag @s add isEliminated
```
///

---

**第6题**，要实现计时器的功能。这个就很简单了：时间变量循环自增1。当到第3秒的时候输出提示，关于各种时间、时刻的描述，可是每次物理的会考杀手啊。第3秒，就是完整经过了2秒，即40游戏刻。所以当计时器变量到40的时候输出提示。然后呢，在对答案的时候，发现量筒想问的是在"3秒末"执行，而按照原题的表述，应该是"3秒初"的意思。因此题目进行了更正。我的作答基于上一版题目。

/// tab | 参考答案
```text {linenums="1"}
/scoreboard players add tick time 1
/execute if score tick time matches 60 run say Hello,world!
```
///

/// tab | 我的作答
/// admonition | 结果分析
    type: success

和答案不一致：问题出在`matches >>40<<`，3秒应该使用60。

`say`部分和答案不一致。
///

```mcfunction {linenums="1"}
scoreboard players add tick time 1
execute if score tick time matches 40 run say 114514 #(1)!
```

1. 按原题目要求，实际上最后一部分应该写成`run say Hello,world!`
///

---

**第7题**，要求当玩家刚进服且有特定标签时输出公告。判断玩家进服的动作，也就是处理退出重进问题。这样一来，问题的答案也就呼之欲出了。

/// tab | 参考答案
```text {linenums="1"}
/scoreboard players add @a isOnline 0
/execute as @a[scores={isOnline=0},tag=vip] run say 欢迎玩家 @s 回到服务器
/scoreboard players reset * isOnline # (1)!
/scoreboard players set @a isOnline 1
```

1. 这里也可以改为`scoreboard objectives remove isOnline`和`scoreboard objectives add isOnline dummy "在线数据"`，以防止追踪对象过多导致运行负载过大
///

/// tab | 我的作答
/// admonition | 结果分析
    type: success

`say`部分和答案不一致。
///

以下代码只需在程序首次运行时执行一次。

```mcfunction {linenums="1"}
scoreboard objectives add isOnline dummy
```

以下代码循环执行。

```mcfunction {linenums="2"}
scoreboard players add @a isOnline 0
execute as @a[tag=vip, scores={isOnline=0}] run say Welcome @s 114514 #(1)!
scoreboard players reset * isOnline
scoreboard players set @a isOnline 1
```

1. 按原题目要求，实际上最后一部分应该写成`run say 欢迎玩家 @s 回到服务器`
///
---

**第8题**，欲实现玩家死了5次之后提示闯关失败的功能。这个直接套用死亡榜的逻辑就行了。但是要注意，题目隐含了一个条件：如果玩家闯关失败了，可能会把玩家送回起点重新开始。因此在玩家死了5次之后需要让死亡次数归零，防止重新开始后再次死亡导致误判。

/// tab | 参考答案
```text {linenums="1"}
/scoreboard players set @a[scores={deathState=!2}] deathState 1
/scoreboard players set @e[type=player] deathState 0
/scoreboard players add @a[scores={deathState=1}] deathCount 1
/scoreboard players set @a[scores={deathState=1}] deathState 2
/execute as @a[scores={deathCount=5..}] run say @s 闯关失败
/scoreboard players set @a[scores={deathCount=5..}] deathCount 0 #(1)!
```

1. 以使玩家能够重新回到游戏。当然，按照题意，也可以不写这条命令。
///

/// tab | 我的作答
/// admonition | 结果分析
    type: question

第7条命令存在语法错误，错误出现在：`es={deathState=1>><<] deathS`。

第8条命令的`say`部分和答案不一致。

第8-9条命令使用了`@e[type=player]`，它会导致玩家在重生后才执行，可能和答案所述的表现有细小差异，但基本是正确的。
///

以下代码只需在程序首次运行时执行一次。

```mcfunction {linenums="1"}
scoreboard objectives add deathState dummy
scoreboard objectives add deathTimes dummy
scoreboard players set @a deathTimes 0
```

以下代码为死亡次数记录部分，循环执行。

```mcfunction {linenums="4" hl_lines="4"}
scoreboard players set @a[scores={deathState=!2}] deathState 1
scoreboard players set @e[type=player] deathState 0
scoreboard players add @a[scores={deathState=1}] deathTimes 1
scoreboard players set @a[scores={deathState=1] deathState 2 # (1)!
```

1. 漏掉了右大括号。正确写法应为`scoreboard players set @a[scores={deathState=1}] deathState 2`。

以下代码为判定游戏失败部分，循环执行，此处使用`say`命令提示游戏失败。

```mcfunction {linenums="8"}
execute as @e[type=player, scores={deathTimes=5..}] run say 114514
scoreboard players remove @e[type=player, scores={deathTimes=5..}] deathTimes 5
```
///

---

**第9题**，要做一个抽奖机，当消耗1个泥土时有0.01%概率出附金（这爆率有点一言难尽啊......）。那么把问题分为两部分：首先是消耗泥土。这里只要求消耗1个泥土，那可以直接使用目标选择器的`hasitem`参数，参照教程要求，为保证代码可读性和简洁性，当存在有泥土的玩家时打一个标签。题目没有交代如何与抽奖机交互，暂使用`@p`来判定抽奖的玩家。接下来是抽奖的环节。0.01%就是一万分之一，那么随机数的范围就是1到10000。要特别注意在开始之前需要对标签和分数进行重置。

/// tab | 参考答案
```text {linenums="1"}
/execute as @p if entity @s[hasitem={item=dirt}] run tag @s add lottery
/execute as @p[tag=lottery] run scoreboard players random lottery data 1 10000
/execute as @p[tag=lottery] if score lottery data matches 1 run give @s enchanted_golden_apple
/execute as @p[tag=lottery] run clear @s dirt
/tag @a remove lottery
```
///

/// tab | 我的作答
/// admonition | 结果分析
    type: question

第6条语法存在语法错误，出现在`execute as @p[tag=hasDirt] run scoreboard players random seed randomSeed >>data<< 1 10000`：应为`int`。

其余部分是正确的。
///

本来后面想全部写`execute as @p if entity @s[tag=...]`的，但是回去查阅教程的时候突然发现可以`execute as @p[tag=...]`一步到位，因为第4行已经可以判断最近的玩家有没有泥土了......不知道为什么当时我大脑宕机了qwq

以下代码只需在程序首次运行时执行一次。

```mcfunction {linenums="1"}
scoreboard objectives add data dummy
```

以下代码在玩家与抽奖机交互时执行。

```mcfunction {linenums="2" hl_lines="5"}
tag @a remove hasDirt
execute as @p if entity @s[hasitem={item=dirt}] run tag add @s hasDirt
execute as @p[tag=!hasDirt] run say Not enough Dirt !
execute as @p[tag=hasDirt] run clear @s dirt
execute as @p[tag=hasDirt] run scoreboard players random seed randomSeed data 1 10000 # (1)!
execute as @p[tag=hasDirt] if scores randomSeed data matches 1145 run give @s enchanted_golden_apple
```

1. 不知因何原因写多了个seed。正确写法为`execute as @p[tag=hasDirt] run scoreboard players random randomSeed data 1 10000`。
///

---

**第10题**，要计算处于死亡状态的人数。这种情况甚至比死亡榜要简单一点：不需要判断玩家是否为刚刚死亡。因此直接使用标签解决。

/// tab | 参考答案
```text {linenums="1"}
/scoreboard players set deadPlayerAmount data 0
/tag @a remove isAlive
/tag @e[type=player] add isAlive
/execute as @a[tag=!isAlive] run scoreboard players add deadPlayerAmount data 1
```
///

/// tab | 我的作答
/// admonition | 结果分析
    type: question

结果正确，但是比参考答案多使用了一条命令，有优化空间。
///

```mcfunction {linenums="1" hl_lines="3-5"}
tag @a remove isDead
scoreboard players set deadPlayerAmount data 0
tag @a add isDead
tag @e[type=player] remove isDead
execute as @a[tag=isDead] run scoreboard players add deadplayerAmount data 1
```

我的作答是先给全部玩家打上标签，再把活的去掉，剩下的就是死亡的了。而答案是直接把活的玩家打上标签，那么若有玩家没有标签，那就是死亡的了。**善用`!`进行反选**！
///

---

### 选做习题	

最后两道题了，我怎么感觉我不是在做题，而是在~~给巴豆和量筒干苦力啊......~~

**第11题**，冰船竞速？赛道成环？3个记录点？划3圈？哇，好熟悉啊，这是在哪里出现过了呢，好难猜啊......  
好了，言归正传。根据原地图的代码（我这算不算作弊......），3圈总计9个记录点，只使用了一个状态变量（范围好像就是0到9）。为了方便后期加圈和代码可读性，我使用两个状态变量，即一个表示圈数、一个表示经过检查点数。"玩家必须划船"说明要满足：玩家在检查点、船在检查点、玩家在船上，才能满足条件。一般情况下，检查点都应该是一个范围，但是嘛，题目也没有给检测的范围，那我也就勉为其难的偷懒一下吧......（然后发现我成了小丑qwq）

/// tab | 参考答案
事实上，这是地图中《冒险世界：筑梦》中的划船小游戏的基本原理。每通过一个记录点，就为`boatRace.@s`添加 1 分，达到 9 分时则通过。依据此原理，可以写出下面的命令。  
首先，先写出经过第一个检查点的命令。在检查点附近检查记录为 0 的玩家，以及玩家附近是否有船，如果有则添加 1 分。

```mcfunction {linenums="1"}
execute positioned -52 60 82 as @a[r=2,scores={boatRace=0}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
```

这样，我们可以把另外 8 个检查点写出来。

```mcfunction {linenums="2" title="参考答案1"}
execute positioned -52 60 82 as @a[r=2,scores={boatRace=0}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -4 60 76 as @a[r=2,scores={boatRace=1}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -63 60 106 as @a[r=2,scores={boatRace=2}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -52 60 82 as @a[r=2,scores={boatRace=3}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -4 60 76 as @a[r=2,scores={boatRace=4}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -63 60 106 as @a[r=2,scores={boatRace=5}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -52 60 82 as @a[r=2,scores={boatRace=6}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -4 60 76 as @a[r=2,scores={boatRace=7}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -63 60 106 as @a[r=2,scores={boatRace=8}] at @s if entity @e[type=boat,r=0.5] run say @s 完成了比赛！
```

现在我们寻求简化命令写法的方法。注意到`boatRace`=`0`、`3`、`6`时，其他执行环境都是类似的，因此我们可以用`boatRace=0..8,boatRace=!1..2,boatRace=!4..5,boatRace=!7..8`的方法来简化几条命令。同样的方法，也可以简化其他命令如下：

```mcfunction {linenums="2" title="参考答案2"}
execute positioned -52 60 82 as @a[r=2,scores={boatRace=0..6,boatRace=!1..2,boatRace=!4..5}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -4 60 76 as @a[r=2,scores={boatRace=1..7,boatRace=!2..3,boatRace=!5..6}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
execute positioned -63 60 106 as @a[r=2,scores={boatRace=2..8,boatRace=!3..4,boatRace=!6..7}] at @s if entity @e[type=boat,r=0.5] run scoreboard players add @s boatRace 1
```

你可以看到上面和我们前面的推理稍微有些差别，但原理是类似的，你可以自行分析。我们还看到`as @a at @s if entity @e[type=boat,r=0.5]`的部分是类似的，都是用于检测玩家附近是否有船，即是否乘船的。这样，我们可以为这样的玩家添加一个标签：

```mcfunction {linenums="2" title="参考答案3"}
tag @a remove ridingBoat
execute as @a at @s if entity @e[type=boat,r=0.5] run tag @s add ridingBoat
execute positioned -52 60 82 as @a[r=2,tag=ridingBoat,scores={boatRace=0..6,boatRace=!1..2,boatRace=!4..5}] run scoreboard players add @s boatRace 1
execute positioned -4 60 76 as @a[r=2,tag=ridingBoat,scores={boatRace=1..7,boatRace=!2..3,boatRace=!5..6}] run scoreboard players add @s boatRace 1
execute positioned -63 60 106 as @a[r=2,tag=ridingBoat,scores={boatRace=2..8,boatRace=!3..4,boatRace=!6..7}] run scoreboard players add @s boatRace 1
```

上面的答案都是可用的，使用何种思路看个人喜好。

///

/// tab | 我的作答
/// warning | 结果分析
--- 语法问题 ---

第4~6行的无效目标选择器参数：`@e[type=boat, x=~, y=~, z=~]`：检查到`x`,`y`,`z`，但是未检查到`dx`,`dy`,`dz`,`r`,`rm`，导致`x`,`y`,`z`无实际意义

--- 逻辑分析 ---

这里因为4~6行的语法问题，导致但凡检测到船，无论玩家周围是否有船都会检查通过，这是偏离预期的。`if entity @s[y=~1.3,  dy=0.1] unless entity @s[y=~1.6, dy=0.1]`应该是要检查玩家是否在船上，但如果正确指定上面的语法问题的话，其实这么做没有那么大的必要。以及，这个地方使用了假名（虚拟玩家），所以可能多人情况下适配情况较差。
///

以下初始化代码只需在程序首次运行时执行一次。

```mcfunction {linenums="1"}
scoreboard objectives add boatRace dummy
```

以下代码需在每次小游戏开始之前执行一次。

```mcfunction {linenums="2"}
scoreboard players set round boadRace 0
scoreboard players set checkpoint boatRace 0
```

以下代码在小游戏进行过程中循环执行，此处使用`say`命令提示已完成划船。

```mcfunction {linenums="4" hl_lines="1-3"}
execute as @a[x=-52, y=60, z=82] at @s if entity @e[type=boat, x=~, y=~, z=~] if entity @s[y=~1.3,  dy=0.1] unless entity @s[y=~1.6, dy=0.1] if score checkpoint boatRace matches 0 run scoreboard players add checkpoint boatRace 1
execute as @a[x=-4, y=60, z=76] at @s if entity @e[type=boat, x=~, y=~, z=~] if entity @s[y=~1.3,  dy=0.1] unless entity @s[y=~1.6, dy=0.1] if score checkpoint boatRace matches 1 run scoreboard players add checkpoint boatRace 1
execute as @a[x=-63, y=60, z=106] at @s if entity @e[type=boat, x=~, y=~, z=~] if entity @s[y=~1.3,  dy=0.1] unless entity @s[y=~1.6, dy=0.1] if score checkpoint boatRace matches 2 run scoreboard players add checkpoint boatRace 1
execute if score checkpoint boatRace matches 3.. run scoreboard players add round boatRace 1
execute if score checkpoint boatRace matches 3.. run scoreboard players remove checkpoint boatRace 3
execute if score round boatRace matches 3.. run say 114514
```
///

---

**第12题**，起床战争？玩家离开？重进已是下一局？好熟悉啊，究竟是在哪里出现过了呢，我怎么突然忘了，好难猜啊......  
好的，真的够了，我怕再继续的话要被各位读者老爷们拉出去斩了......如果你玩过*量筒的起床战争*的话，那你一定玩过*量筒*的*起床战争*。（不是废话文学，注意第二个的斜体啊......）细心的话就会留意到，在每局的游戏模式右边，有个灰色的4位数字，不仔细看还真看不出来，这就是所谓的"游戏ID"。  
何为"游戏ID"？就是对当前对局的一个编号。和处理"玩家下线"问题的思路类似，可以设一个记分项，在每局开始的时候，系统指定一个数字（即游戏ID），同时把此时在线的玩家在该记分项上的分数设为本局的ID。如果有玩家下线，并且在回来的时候下一局已经开了，那么他会错过第二局分配游戏ID的步骤，这是他在记分项上的分数就是上一局的ID，和新对局的ID匹配不上，就可以识别出来了。  
量筒的地图中，产生游戏ID的方式是从1000到9999中随机抽一个数。但是你说不准，就刚好碰上那1/9000的机会，有人退的那一把和当前对局的游戏ID一样，导致误判。所以我将游戏ID从1开始设起，每局结束后递增，顺便也能看看累计开了多少局（喜）。

/// tab | 参考答案
如果什么都不做，很显然这样的玩家加入后会错误地加入到下一局已经分配好的队伍中，导致队伍中凭空多出一人。而且，如果加入了本局中本不存在的队伍，有导致程序崩溃的风险。因此，必须处理退出重进的玩家的数据。

方法是，可以为每一局的游戏分配一个`gameId`，并让每名玩家分配与本局`gameId`相同的`gameId`。如果`gameId`不相同，则阻止玩家加入进游戏中，并移除该玩家的队伍信息等信息。这样，就可以防止退出重进的玩家影响下一局的情况发生。

首先是，对每局随机一个`gameId`。为了随机不会与以前重复，随机的范围要大，这里取 1000~9999。

```mcfunction
scoreboard players random this gameId 1000 9999
scoreboard players operation @a gameId = this gameId
```

这样，正常情况下，`gameId.@s`=`gameId.this`，其中`gameId.this`代表本局的`gameId`，而`gameId.@s`代表玩家的`gameId`。

如果玩家的`gameId`不符，则将其设为旁观者，并移除其其他信息。

```mcfunction
execute as @a unless score @s gameId = this gameId run gamemode spectator @s
execute as @a unless score @s gameId = this gameId run (移除数据的命令)
execute as @a unless score @s gameId = this gameId run scoreboard players operation @a gameId = this gameId
```

当然，这些可以放在退出重进的玩家的检测中。综上，只需要将以下的命令循环执行即可。

```mcfunction {linenums="1" title="本题参考答案"}
scoreboard players random this gameId 1000 9999
scoreboard players operation @a gameId = this gameId

scoreboard players add @a isOnline 0
execute as @a[scores={isOnline=0}] unless score @s gameId = this gameId run gamemode spectator @s
execute as @a[scores={isOnline=0}] unless score @s gameId = this gameId run (移除数据的命令)
execute as @a[scores={isOnline=0}] unless score @s gameId = this gameId run scoreboard players operation @a gameId = this gameId
scoreboard players reset * isOnline # (1)!
scoreboard players set @a isOnline 1
```

1. 或者可以使用下面两条命令  
  `scoreboard objectives remove isOnline`  
  `scoreboard objectives add isOnline dummy "在线数据"`

///

/// tab | 我的作答
/// admonition | 结果分析
    type: question

--- 语法问题 ---

第2行存在语法错误，出现在`scoreboard objectives add playerGameId >><<`，应为一个有效的准则。  
第7行存在语法错误，出现在`if score @s playGameId >><< < gameId data`，应为`string`。

---

这里采用了每局`gameId`递加的方法，所以使用`<`检测是没问题的，但个人更推荐用`unless` ... `=`的方法
///

以下初始化代码只需在程序首次运行时执行一次。

```mcfunction {linenums="1" hl_lines="2"}
scoreboard objectives add data dummy
scoreboard objectives add playerGameId # (1)!
scoreboard players set gameId data 0
```

1. 此行命令正确写法应为`scoreboard objectives add playerGameId dummy`。

以下代码需在每次对局开始前执行一次。

```mcfunction {linenums="4"}
scoreboard players add gameId data 1
scoreboard players operation @a playerGameId = gameId data
```

以下代码在对局过程中循环执行，此处使用`midwayJoined`标签标记后进的玩家。

```mcfunction {linenums="6" hl_lines="2"}
tag @a remove midwayJoined
execute as @a if score @s playGameId < gameId data run tag @s add midwayJoined # (1)!
```

1. 若使用`unless`，则命令为`execute as @a unless score @s playGameId = gameId data run tag @s add midwayJoined`。
///

## 结语

做完这12道题之后，我才发现，我好菜啊qwq......不幸中的万幸是，我出错的大部分都是语法错误，在此我再借用一下量筒的话：

> 因为会导致函数等编译错误，内容日志会报错，错误类型最明显，所以是最好解决的问题。

但是嘛......第11题那个就是纯纯的逻辑错误了，前面的选择器参数有些记不清了，而这种问题还不会报错，以后写命令时要更加小心，避免这类错误。但是并不是谁都能一直记住全部命令和参数，因此在不确定的时候要多翻一下教程和wiki。不管怎样，学了这么多，最后还是要回到实际工程上面，毕竟理论学得再好，脱离了实践也毫无用处。

最后，再次再次再次感谢你能够看到这里！这一节出现了许多在实际工程中会用到的东西，而且知识量也是超级大，所以要写的东西自然也就多了。

既然你都看到这里了，那再告诉你一个*热知识*吧：这两篇博客的超长字数估计要成为绝唱咯。毕竟execute和scoreboard这命令界两大活爹已经过去了，后面的难度也是有手就行了。话说回来，后面学习的进度要加快了，毕竟教程都更新到第3章了啊......
